/*
 * Gradle build file for jHDF
 */
plugins {
    // Core plugins
	id 'java-library'
    id 'jacoco' // Code coverage
    id 'eclipse' // To create eclipse project
    id 'idea' // To create InteliJ project
    id 'maven-publish'
    // Community plugins (need versions)
    id 'com.jfrog.bintray' version '1.8.4' // For publishing to Bintray
    id 'org.sonarqube' version '2.6.2' // Code quality
}

// Varibles
group = 'io.jhdf'
version = '0.1.1'
sourceCompatibility = 1.8
targetCompatibility = 1.8

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            afterEvaluate {  
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }
}

bintray {
    user = 'jamesmudd'
    key = '9eafb69bcc49b6db8f43572a2b448875b9d7ec4f'
    pkg {
        repo = 'jhdf'
        name = 'jhdf'
        userOrg = 'jamesmudd'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/jamesmudd/jhdf.git'
        version {
            name = project.version
            desc = 'jHDF'
            released  = new Date()
        }
    }
    // Which files to upload where
    filesSpec {
       from 'build/libs'
       into project.version
    }
}

// In this section you declare where to find the dependencies of your project
repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}

dependencies {
    // This dependency is exported to consumers, that is to say found on their compile classpath.
    // api 'org.apache.commons:commons-math3:3.6.1'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.8.0-beta2' // Need 1.8 for modules
    compile group: 'ch.qos.logback', name:'logback-classic', version: '1.2.3'
    compile group: 'ch.qos.logback', name:'logback-core', version: '1.2.3'

    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
    // implementation 'com.google.guava:guava:21.0'

    // Use JUnit test framework
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.23.4'
    
    // https://mvnrepository.com/artifact/org.hamcrest/hamcrest-all
	testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                   'Implementation-Version': project.version,
                   // OSGi headers
                   'Bundle-ManifestVersion': '2',
                   'Bundle-SymbolicName': 'io.jhdf',
                   'Bundle-Name': project.name,
                   'Bundle-Vendor': 'James Mudd',
                   'Export-Package': 'io.jhdf.*'
                   )
        exclude('io/jhdf/examples')
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}